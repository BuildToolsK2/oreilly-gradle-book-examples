apply plugin: 'java'

repositories {
    mavenCentral()
}

dependencies {
    testCompile 'junit:junit:4.8.2'
}

test {
    maxParallelForks = 3
    forkEvery = 5
}

// Thanks to mrhaki for the original web-broadast idea
// http://mrhaki.blogspot.com/2010/11/gradle-goodness-running-tests-in.html
task createTests << {
    (0..5).each { packageCounter ->
        def packageName = "sample${packageCounter}"
        (0..10).each { classCounter ->
            def testClassName = "TestPerson${classCounter}"
            copy {
                from 'src/template/java'
                into 'src/test/java'
                expand([packageName: packageName, testClassName: testClassName])
                rename '(.*).java', packageName + '/' + testClassName + '.java'
                include '**/TestPerson.java'
            }
        }
    }
}
